-- @atlcompiler emftvm
-- @path REcore=/ATL/model/RefinementsEcore.ecore
-- @path Code=/ATL/model/SimpleCodeDOM.ecore
module transformationB;
create OUT : Code from IN : REcore;

uses transformationA;

helper context REcore!EReference def: getterName : String =
	'get_' + self.name;


-- redefined rule
rule EClass2Interface {
	from
		s : REcore!EClass
	to
		t : Code!Class (
			IsInterface <- true,
			Name <- s.name,
			BaseTypes <-	s.eSuperTypes->collect(c |
								thisModule.IfaceRef(c)
							),
			Members <- s.eStructuralFeatures
		),
		impl : Code!Class (
			IsInterface <- false,
			Name <- s.implName,
			-- Change 1: added additional part for creating the Type Ref to the DBObject
			BaseTypes <-	s.superImpl->collect(c |
								thisModule.ImplRef(c)
							)->union(
								if s.superImpl->isEmpty() then
									Sequence {thisModule.createTypeRef('DBObject')}
								else
									Sequence{}
								endif
							)->append(
									thisModule.IfaceRef(s)
							),
			-- Change 2: deleted the creation of fields + added the delegatingPropImpl
			Members <-
				s.featuresToImpl->collect(r |
					if r.refiningFeatureIn(s).oclIsUndefined() then
						thisModule.PropImpl(s, r)
					else
						thisModule.DelegatingPropImpl(s, r)
					endif
				)->flatten()
		)
}

-- redefined rule
rule Reference2Property {
	from
		s : REcore!EReference
	to
		t : Code!Method (
			IsAbstract <- true,
			-- Change 3: redefinition of name binding
			Name <- s.getterName,
			ReturnType <- thisModule.IfaceRef(s.eType)
		)
}

-- redefined rule
unique lazy rule PropImpl {
	from
		c : REcore!EClass,
		r : REcore!EReference
	to
		-- Change 4: output pattern element type changed from Property to Method
		t : Code!Method (
			IsAbstract <- false,
			Name <- r.getterName,
			BodyExpression <- get,
			ReturnType <-  thisModule.IfaceRef(r.eType)
		),
		get : Code!MethodInvokeExpression (
			Arguments <- Sequence{thg, se},
			MethodName <- 'resolve'
		),
		thg : Code!ThisReferenceExpression,
		se : Code!StringLiteralExpression(
			Value <- r.name	
		)
}

-- new rule
unique lazy rule DelegatingPropImpl {
	from
		c : REcore!EClass,
		r : REcore!EReference
	to
		t : Code!Method (
			IsAbstract <- false,
			Name <- r.getterName,
			BodyExpression <- get,
			ReturnType <-  thisModule.IfaceRef(r.eType)
		),
		get : Code!MethodInvokeExpression (
			TargetObject <- thg,
			MethodName <- r.refiningFeatureIn(c).getterName
		),
		thg : Code!ThisReferenceExpression
}

-- new rule
rule createTypeRef(name : String) {
	to
		t : Code!TypeReference (
			BaseName <- name
		)
	do{ t; }
}
